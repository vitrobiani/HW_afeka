%{
//  tennis_tab.h was generated by bison
#include "tennis.tab.h"

#include <string.h>
#include <stdlib.h> 

%}

%option noyywrap
%option yylineno

/* exclusive start condition for handling comments */ 
%x STAR_COMMENT

%%
"** Winners **" { return TITLE; }

"<name>"     { return NAME; }

"<Woman>"      { yylval.gender = WOMAN; return GENDER; }
"<Man>"        { yylval.gender = MAN; return GENDER; }

"<Wimbledon>"       { return WIMBLEDON; }
"<Australian Open>" { return AUSTRALIAN_OPEN; } 
"<French Open>"     { return FRENCH_OPEN; }

[0-9]+ { yylval.year = atoi(yytext); return YEAR; }

["'][A-Z][a-z]*(" "[A-Z][a-z]*)?["']  { // copy name without the surrounding quotes
           strcpy (yylval.name, yytext+1);
           yylval.name[strlen(yylval.name)-1] = '\0';
           return PLAYER_NAME; }
		   
"-"       { return '-';}
","       { return ','; }

[\t\r\n ]+  { /* skip white space */ }

"/*"        { BEGIN(STAR_COMMENT); }
<STAR_COMMENT>.  { /* skip character inside comment */ }
<STAR_COMMENT>"*/" { BEGIN(0); }

"//".*      { /* skip comment starting with "//" */ }

       
. { fprintf (stderr, "line %d: unrecognized token %c (%x)\n",
                      yylineno, yytext[0], yytext[0]); }
					  
%%

